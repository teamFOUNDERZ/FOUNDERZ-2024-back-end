plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

springBoot {
    mainClass = 'com.founderz.Application'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'

    group = 'com.founderz'
    version = '0.0.1-SNAPSHOT'

    java {
        sourceCompatibility = '21'
    }

    repositories {
        mavenCentral()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        if (project.name != "founderz-common") {
            implementation 'org.springframework.boot:spring-boot-starter-web'

            compileOnly 'org.projectlombok:lombok'
            annotationProcessor 'org.projectlombok:lombok'
            implementation 'org.mapstruct:mapstruct:1.6.0'
            annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.0'
        }
    }

    if (project.name == "founderz-boot") {
        jar.enabled = false
        bootJar.enabled = true
    } else {
        jar.enabled = true
        bootJar.enabled = false
    }

    test {
        useJUnitPlatform()
    }

}

jar.enabled = true
bootJar.enabled = false